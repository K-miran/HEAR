# Copyright (c) Microsoft Corporation. All rights reserved.
# Licensed under the MIT license.

# Exports target SEAL::seal
#
# Creates variables:
#   SEAL_FOUND : If Microsoft SEAL and all required dependencies were found
#   SEAL_BUILD_TYPE : The build configuration used
#   SEAL_DEBUG : Set to non-zero value if library is compiled with extra debugging code (very slow!)
#   SEAL_LIB_BUILD_TYPE : Set to either "Static", "Static_PIC", or "Shared" depending on library build type
#   SEAL_USE_CXX17 : Set to non-zero value if library is compiled as C++17 instead of C++14
#   SEAL_ENFORCE_HE_STD_SECURITY : Set to non-zero value if library is compiled to enforce at least
#       a 128-bit security level based on HomomorphicEncryption.org security estimates
#   SEAL_USE_MSGSL : Set to non-zero value if library is compiled with Microsoft GSL support
#   SEAL_USE_ZLIB : Set to non-zero value if library is compiled with zlib support
#   MSGSL_INCLUDE_DIR : Holds the path to Microsoft GSL if library is compiled with Microsoft GSL support

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

macro(warning_when_not_quiet msg)
    if(NOT SEAL_FIND_QUIETLY)
        message(WARNING ${msg})
    endif()
endmacro()

macro(status_when_not_quiet msg)
    if(NOT SEAL_FIND_QUIETLY)
        message(STATUS ${msg})
    endif()
endmacro()

macro(find_seal_dependency dep)
    find_dependency(${dep})
    if(NOT ${dep}_FOUND)
        warning_when_not_quiet("Could not find dependency `${dep}` required by this configuration")
        set(SEAL_FOUND FALSE)
        return()
    endif()
endmacro()

set(SEAL_FOUND FALSE)

set(SEAL_BUILD_TYPE @CMAKE_BUILD_TYPE@)
set(SEAL_DEBUG @SEAL_DEBUG@)
set(SEAL_LIB_BUILD_TYPE @SEAL_LIB_BUILD_TYPE@)
set(SEAL_USE_CXX17 @SEAL_USE_CXX17@)
set(SEAL_ENFORCE_HE_STD_SECURITY @SEAL_ENFORCE_HE_STD_SECURITY@)

# Add the current directory to the module search path
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

set(SEAL_USE_MSGSL @SEAL_USE_MSGSL@)
if(SEAL_USE_MSGSL)
    find_seal_dependency(MSGSL)
endif()

set(CMAKE_THREAD_PREFER_PTHREAD TRUE)
set(THREADS_PREFER_PTHREAD_FLAG TRUE)
find_seal_dependency(Threads)

set(SEAL_USE_ZLIB @SEAL_USE_ZLIB@)
if(SEAL_USE_ZLIB)
    find_seal_dependency(ZLIB)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/SEALTargets.cmake)

if(TARGET SEAL::seal)
    status_when_not_quiet("Microsoft SEAL -> Version ${SEAL_VERSION} detected")
    if(SEAL_DEBUG)
        status_when_not_quiet("Performance warning: Microsoft SEAL compiled in debug mode")
    endif()
    status_when_not_quiet("Microsoft SEAL -> Library build type: ${SEAL_LIB_BUILD_TYPE}")
    set(SEAL_FOUND TRUE)
else()
    warning_when_not_quiet("Microsoft SEAL -> NOT FOUND")
    set(SEAL_FOUND FALSE)
endif()

